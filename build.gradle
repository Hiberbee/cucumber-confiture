plugins {
  id("com.github.ben-manes.versions") version "0.28.0"
  id("com.google.cloud.tools.jib") version '2.3.0'
  id("idea")
  id("jacoco")
  id("java-library")
  id("maven-publish")
  id("org.jetbrains.gradle.plugin.idea-ext") version "0.7"
  id("org.sonarqube") version "3.0"
}

group "com.hiberbee.cucumber"
description "Monorepo entrypoint for Cucumber test packages"
version "1.0.0-SNAPSHOT"

def VERSIONS = [
  cucumber            : "6.0.0-RC2",
  cucumberReporting   : "4.0.53",
  gradle              : "6.5-rc-1",
  guava               : "29.0-jre",
  jetbrainsAnnotations: "19.0.0",
  junit               : "5.7.0-M1",
  netty               : "4.1.49.Final",
  lombok              : "1.18.12",
  springBoot          : "2.3.0.RELEASE",
]

wrapper {
  distributionType = Wrapper.DistributionType.ALL
  gradleVersion = VERSIONS.gradle
}

idea {
  project {
    settings {
      copyright {
        useDefault = "MIT"
        profiles {
          MIT {
            notice = file("LICENSE").readLines().join("\n")
          }
        }
      }
    }
  }
  module {
    downloadJavadoc = false
    downloadSources = true
    excludeDirs += files("gradle", "stress", ".gradle")
    inheritOutputDirs = true
    outputDir = file("build")
  }
}

repositories {
  mavenCentral()
}

sonarqube {
  properties {
    property "sonar.login", System.getProperty("sonar.token")
    property "sonar.host.url", System.getProperty("sonar.url")
    property "sonar.projectKey", rootProject.name
    property "sonar.organization", System.getProperty("organization")
    property "sonar.links.homepage", System.getProperty("vcs.url")
  }
}

class CucumberFeature extends JavaExec {

  CucumberFeature() {
    super.setGroup("verification")
    super.dependsOn(project.tasks.getByName("testClasses"))
    super.outputs.dir("build/reports")
    super.setMain("org.junit.platform.console.ConsoleLauncher")
    super.
      setClasspath(project.
        convention.
        getPlugin(JavaPluginConvention.class).
        getSourceSets().
        findByName("test").
        runtimeClasspath)
    super.args("--include-engine", "cucumber", "--reports-dir", "build/reports/cucumber")
    super.finalizedBy(project.tasks.getByName("jacocoTestReport"))
  }
}

allprojects {
  group = rootProject.group
  version = rootProject.version

  apply plugin: "idea"
  apply plugin: "jacoco"
  apply plugin: "java-library"
  apply plugin: "org.sonarqube"
  apply plugin: "org.jetbrains.gradle.plugin.idea-ext"

  jacoco {
    toolVersion = "0.8.5"
  }

  idea {
    module {
      downloadJavadoc = false
      downloadSources = true
      excludeDirs += files("gradle", ".gradle")
      inheritOutputDirs = true
      outputDir = file("build")
    }
  }

  tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    it.options.setFork(true)
    it.options.setIncremental(true)
    dependsOn(tasks.named("processResources"))
  }

  tasks.withType(Test).configureEach {
    useJUnitPlatform {
      failFast = true
    }
    scanForTestClasses(false)
    jacoco {
      enabled = true
      dumpOnExit = true
    }
    finalizedBy(tasks.named("jacocoTestReport"))
  }

  tasks.withType(JacocoCoverageVerification).configureEach {
    enabled = true
  }

  tasks.withType(JacocoReport).configureEach {
    enabled = true
    reports {
      xml.enabled = true
      html.enabled true
      csv.enabled false
    }
  }

  configurations {
    compileOnly {
      extendsFrom(annotationProcessor)
    }
  }

  dependencies {
    implementation("com.google.guava:guava") {
      version {
        strictly VERSIONS.guava
      }
    }
    implementation platform("org.junit:junit-bom:$VERSIONS.junit")
    implementation platform("io.cucumber:cucumber-core:$VERSIONS.cucumber")
    implementation platform("org.springframework.boot:spring-boot-dependencies:$VERSIONS.springBoot")

    compileClasspath("org.jetbrains:annotations:$VERSIONS.jetbrainsAnnotations")
    testImplementation("de.monochromata.cucumber:reporting-plugin:$VERSIONS.cucumberReporting")
    testImplementation("io.cucumber:cucumber-junit-platform-engine")
    testImplementation("org.junit.jupiter:junit-jupiter-api")
    testImplementation("org.junit.jupiter:junit-jupiter-engine")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
    testRuntimeOnly("org.junit.platform:junit-platform-console")
    testRuntimeOnly("org.junit.platform:junit-platform-runner")
  }
}

subprojects {

  apply plugin: "maven-publish"

  tasks.register("features", CucumberFeature) {
    args += [
      "--select-package",
      "features",
    ]
  }

  publishing {
    repositories {
      maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/hiberbee/cucumber-confiture")
        credentials {
          username = System.getenv("GITHUB_USERNAME")
          password = System.getenv("GITHUB_TOKEN")
        }
      }
    }
    publications {
      gpr(MavenPublication) {
        from(components.java)
      }
    }
  }
}
